export class List<T>:
    T[] elements;
    int32 size := 0;

    export static create(int32 startSize:=5) -> List<T> {
        res:List<T> = new List<T>(elements = new T[startSize]);
        return res;
    }

    export elements() -> T[] {
        return elements; //TODO if we have proper handling of fields in the instantiated shims this is no longer necessary
    }

    export size() -> int32 {
        return size; //TODO if we have proper handling of fields in the instantiated shims this is no longer necessary
    }

    export add(T e) {
        if size >= elements.size {
            expand();
        }
        elements[size] = e;
        ++size;
    }

    export add(T e, int32 pos) {
        if size >= elements.size {
            expand();
        }
        rShift(pos);
        elements[pos] = e;
        ++size;
    }

    export put(T e, int32 pos) {
        elements[pos] = e;
    }

    export get(int32 i) -> T {
        return elements[i];
    }

    export clear() {
        size=0;
    }

    export remove() -> T {
        --size;
        return elements[size];
    }

    export remove(int32 pos) -> T {
        res:T = elements[pos];
        lShift(pos);
        --size;
        return res;
    }

    private expand() {
        newSize:int32 = 2*elements.size;
        if newSize < 10 {
            newSize = 10;
        }

        newElements := new T[newSize];
        arrayCopy(elements, newElements);
        elements = newElements;
    }

    private rShift(int32 start) { //TODO replace by arrayCopy... once we have that
        i := size;
        while i>start {
            elements[i] = elements[i-1];
            --i;
        }
    }

    private lShift(int32 end) { //TODO replace by arrayCopy... once we have that
        i := end;
        while i<size-1 {
            elements[i] = elements[i+1];
            ++i;
        }
    }

    private static arrayCopy(T[] from, T[] to) { //TODO replace by arrayCopy... once we have that
        i:int32 = 0;
        while i<from.size {
            to[i] = from[i];
            ++i;
        }
    }